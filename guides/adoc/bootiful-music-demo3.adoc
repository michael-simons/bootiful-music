= Some advanced queries

== Implicit grouping

[source,cypher,subs=attributes]
----
MATCH (s:SoloArtist)<-[:HAS_MEMBER]-()
WITH s, count(*) as bands ORDER BY bands limit 5
RETURN s, bands
----

== Just counting relationships

[source,cypher,subs=attributes]
----
MATCH (s:SoloArtist) 
RETURN s, size((s)<-[:HAS_MEMBER]-()) as bands
ORDER BY bands desc limit 5
----

== Also retrieve the data

[source,cypher,subs=attributes]
----
MATCH (a:SoloArtist)
WHERE size((a)<- [:HAS_MEMBER] - ()) >= 2
WITH a
MATCH p=(a)<-[:HAS_MEMBER]-()
RETURN p
----


== Using virtual relationships

[source,cypher,subs=attributes]
----
MATCH  (a:SoloArtist) <- [m:HAS_MEMBER] - (b)
WITH a, collect(distinct b) as bands
WHERE size(bands) >= 2
UNWIND bands as b
OPTIONAL MATCH p=(a) - [:ASSOCIATED_WITH] - (o)
RETURN p,a,b, apoc.create.vRelationship(a,'IS_MEMBER_OF',{},b) as r
----

== Creating micro genres

[source,cypher,subs=attributes]
----
MATCH (a)-[:HAS]->(g:Genre), 
      (a)-[:RELEASED_IN]->(:Year)-[:PART_OF]->(d:Decade), 
      (a)-[:RELEASED_BY]->(artist)-[:FOUNDED_IN|:BORN_IN]->(c:Country)
WHERE c.name IS NOT NULL
WITH g, d,c, count(*) as freq, collect(a) as albums
ORDER by freq desc limit 200
WITH albums, freq, apoc.create.vNode(['Cat'], {name:d.value + " " + c.name + " " + g.name}) as cat
UNWIND albums as a
RETURN cat, a, apoc.create.vRelationship(a,'IN',{freq:freq},cat) as rel
----

== Recommendations (1)

[source,cypher,subs=attributes]
----
:params {artist: 'Queen'}
----

First "score" by genre

[source,cypher,subs=attributes]
----
MATCH (s:Artist {name:$artist})
MATCH (s)<-[:RELEASED_BY]-()-[:HAS]->(g:Genre)<-[:HAS]-()-[:RELEASED_BY]->(a)
WHERE a<>s
WITH s,a, count(*) as genreScore order by genreScore desc
RETURN s.name, a.name, genreScore
----


== Recommendations (2)

Add additional aspects

[source,cypher,subs=attributes]
----
MATCH (s:Artist {name:$artist})
MATCH (s)<-[:RELEASED_BY]-()-[:HAS]->(g:Genre)<-[:HAS]-()-[:RELEASED_BY]->(a)
WHERE a<>s
WITH s,a, count(*) as genreScore

MATCH (s)<-[:RELEASED_BY]-()-[:RELEASED_IN]->(:Year)<-[:RELEASED_IN]-()-[:RELEASED_BY]->(a)
WITH s,a,genreScore, count(*) as yearScore

RETURN s.name, a.name, genreScore*0.1 + yearScore *0.5 as score
ORDER BY score desc limit 10;
----


== Recommendations (3)

Add additional aspects

MATCH (s:Artist {name:$artist})
MATCH (a:Artist {name:'Bela B.'})
OPTIONAL MATCH (s)-[:FOUNDED_IN]->()<-[:FOUNDED_IN]-(a)
WITH s,a, count(*)

[source,cypher,subs=attributes]
----
MATCH (s:Artist {name:$artist})
MATCH (s)<-[:RELEASED_BY]-()-[:HAS]->(g:Genre)<-[:HAS]-()-[:RELEASED_BY]->(a)
WHERE a<>s
WITH s,a, count(*) as genreScore

MATCH (s)<-[:RELEASED_BY]-()-[:RELEASED_IN]->(:Year)<-[:RELEASED_IN]-()-[:RELEASED_BY]->(a)
WITH s,a,genreScore, count(*) as yearScore

WITH s,a,genreScore,yearScore, 
     EXISTS((s)-[:FOUNDED_IN]->()<-[:FOUNDED_IN]-(a)) as foundedInSameCountry

WITH s,a,genreScore,yearScore, 
     CASE foundedInSameCountry WHEN true THEN 1.1 ELSE 0.9 END as bump

RETURN s.name, a.name, (genreScore*0.1 + yearScore *0.25)*bump  as score
ORDER BY score desc limit 10;
----


== Connections

[source,cypher,subs=attributes]
----
MATCH (a:Artist {name:"Slayer"})
MATCH (a2:Artist {name:'Iced Earth'})
MATCH path=shortestPath( (a)-[:ASSOCIATED_WITH*]-(a2))
RETURN a.name, a2.name, path limit 10
----

[source,cypher,subs=attributes]
----
MATCH (n:Band) where n.name IN ['Slayer', 'Metallica', 'Megadeth', 'Exodus']
OPTIONAL MATCH (n) - [:HAS_MEMBER] -> (s:SoloArtist)
WITH collect(n) + collect(s) as nodes
UNWIND nodes as n
UNWIND nodes as m
WITH * WHERE id(n) < id(m)
MATCH path = allShortestPaths( (n)-[*..2]-(m) )
RETURN path
----